import java.text.SimpleDateFormat

plugins {
    id 'name.remal.check-dependency-updates' version '1.0.211'  // and run "checkDependencyUpdates" task
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

static def polyEpoch() {
    // Seconds since 2019-05-04 00:00 UTC - to avoid the year 2038 problem.
    def now = new Date()
    def df = new SimpleDateFormat("yyyy-MM-dd")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    def polypolyFounding = df.parse("2019-05-04")
    return (int) Math.floor((now.getTime() - polypolyFounding.getTime()) / 1000)
}


ktlint {
    debug = true
    verbose = true
    reporters {
        reporter "checkstyle"
        reporter "plain"
    }
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "coop.polypoly.polypod"
        minSdkVersion 24 // as long as we use the system WebView, going lower would mean a lot of limitations for feature developers
        targetSdkVersion 32
        versionCode polyEpoch()
        versionName "1.5.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (project.hasProperty("sign")) {
            playStoreUpload {
                storeFile file(String.valueOf(System.getenv("POLYPOD_ANDROID_UPLOAD_KEYSTORE_FILE")))
                storePassword System.getenv("POLYPOD_ANDROID_UPLOAD_KEYSTORE_PASS")
                keyAlias System.getenv("POLYPOD_ANDROID_UPLOAD_KEY_ALIAS")
                keyPassword System.getenv("POLYPOD_ANDROID_UPLOAD_KEYSTORE_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            if (project.hasProperty("sign")) {
                signingConfig signingConfigs.playStoreUpload
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            assets.srcDirs += file('build/generated-assets')
            java.srcDirs += file('src/main/kotlin')
            java.srcDirs += file('src/main/java')
        }
        test {
            java.srcDirs += file('src/test/kotlin')
            java.srcDirs += file('src/test/java')
        }
        androidTest {
            java.srcDirs += file('src/androidTest/kotlin')
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                systemProperty 'robolectric.logging.enabled', 'true'
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                }
            }
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }

}

repositories {
    // used for carouselView
    jcenter()
}

dependencies {
    implementation project(":polyOut")
    implementation project(":core")
    implementation 'org.msgpack:msgpack-core:0.8.20'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.webkit:webkit:1.4.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.synnapps:carouselview:0.1.5'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation group: 'org.apache.jena', name: 'jena-core', version: '3.17.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha04'

    // this unfortunately is needed to test the app
    debugImplementation 'androidx.test:core:1.4.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.4.1'

    // dependencies for tests that do not require Android device/emulator running
    testImplementation 'junit:junit:4.13'   // yuck...
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    // fragment-testing requires entry with 'debugImplementation' scope, but also with 'testImplementation'
    // linter tells more (hence ignoring FragmentGradleConfiguration linter option)
    //noinspection FragmentGradleConfiguration
    testImplementation 'androidx.fragment:fragment-testing:1.4.1'

    // for bubblewrap
    testImplementation 'org.graalvm.js:js:20.1.0'
    testImplementation 'net.jqwik:jqwik:1.3.3'
    testImplementation 'org.assertj:assertj-core:3.17.1'

    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.30'

    // instrumented tests dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    // fragment-testing requires entry with 'debugImplementation' scope, but also with 'androidTestImplementation'
    // linter tells more (hence ignoring FragmentGradleConfiguration linter option)
    //noinspection FragmentGradleConfiguration
    androidTestImplementation 'androidx.fragment:fragment-testing:1.2.5'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'

    // Encryption related imports
    implementation("androidx.security:security-crypto:1.1.0-alpha03")
    // For Identity Credential APIs
    implementation("androidx.security:security-identity-credential:1.0.0-alpha03")
    // For App Authentication APIs
    implementation("androidx.security:security-app-authenticator:1.0.0-alpha02")
    // For App Authentication API testing
    androidTestImplementation("androidx.security:security-app-authenticator:1.0.0-alpha02")
}

task copyRemotePod(type: Copy) {
    def path = "../../feature-api/communication/dist/bootstrap.js"
    assert file(path).exists()
    from path
    rename "bootstrap.js", "pod.js"
    into "build/generated-assets/container/"
}

task copyBundledFeatures(type: Copy) {
    def path = "../../../features/bundle/dist"
    assert file(path).exists()
    from path
    into "build/generated-assets/features"
}

task copyThirdPartyLicenses(type: Copy) {
    def path = "../../../3rd-party-licenses"
    assert file(path).exists()
    from path
    into "build/generated-assets/3rd-party-licenses"
}

task installAppOnDevice {
    // This is needed to install the Java package into the device
    dependsOn "assembleDebug"
    doLast {
        exec {
            logger.info("Trying to install app")
            def adb = android.getAdbExecutable().absolutePath
            commandLine adb, 'install', "$buildDir/outputs/apk/debug/app-debug.apk"
            logger.info("Application installed onto the device")
        }
    }
}

task copyEndpoints(type: Copy) {
    def path = "../../../../polyPod-config"
    assert file(path).exists()
    from path
    into "build/generated-assets/config-assets"
}

task packageTestFeature(type: Zip) {
    // This needs the prior installation of the package, thus the dependency
    dependsOn installAppOnDevice
    archiveFileName = "test.zip"
    destinationDirectory = file("$buildDir")
    from "$projectDir/../../../features/test/dist"
}

task copyTestFeatureOntoDevice {
    dependsOn packageTestFeature
    def adb = android.getAdbExecutable().absolutePath
    def tempPath = "/data/local/tmp/test.zip"
    def packageName = android.defaultConfig.applicationId
    def featurePath = "files/features"
    doLast {
        exec {
            commandLine adb, 'push', "--sync", "$buildDir/test.zip", tempPath
            logger.info("test feature copied onto the device")
        }
        exec {
            commandLine adb, 'shell', "run-as", packageName, "mkdir", '-p', featurePath
            logger.info("Feature directory created on the device")
        }
        exec {
            commandLine adb, 'shell', "run-as", packageName, "cp", tempPath, featurePath
            logger.info("test feature moved to features directory on the device")
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == "generateDebugAssets" || task.name == "generateReleaseAssets") {
        task.dependsOn copyRemotePod
        task.dependsOn copyBundledFeatures
        task.dependsOn copyThirdPartyLicenses
        task.dependsOn copyEndpoints
    }

    // This avoids circular dependencies in those cases
    if (
        task.name == "connectedDebugAndroidTest" ||
        task.name == "connectedReleaseAndroidTest" ||
        task.name == "generateDebugAndroidTestAssets"
    )
        task.dependsOn copyTestFeatureOntoDevice
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        // Generated FlatBuffer models use ExperimentalUnsignedTypes
        freeCompilerArgs += "-opt-in=kotlin.ExperimentalUnsignedTypes"
    }
}
