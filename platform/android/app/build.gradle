plugins {
    id 'name.remal.check-dependency-updates' version '1.0.211'  // and run "checkDependencyUpdates" task
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

static def polyEpoch() {
    // Seconds since 2019-05-04 00:00 UTC -- to avoid the year 2038 problem.
    def now = new Date()
    def polypolyFounding = Date.parse("yyyy-MM-dd", "2019-05-04", TimeZone.getTimeZone("UTC"))
    return (int) Math.floor((now.getTime() - polypolyFounding.getTime()) / 1000)
}


ktlint {
    debug = true
    verbose = true
    reporters {
        reporter "checkstyle"
        reporter "plain"
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "coop.polypoly.polypod"
        minSdkVersion 24 // as long as we use the system WebView, going lower would mean a lot of limitations for feature developers
        targetSdkVersion 30
        versionCode polyEpoch()
        versionName "1.5.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (project.hasProperty("sign")) {
            playStoreUpload {
                storeFile file(String.valueOf(System.getenv("POLYPOD_ANDROID_UPLOAD_KEYSTORE_FILE")))
                storePassword System.getenv("POLYPOD_ANDROID_UPLOAD_KEYSTORE_PASS")
                keyAlias System.getenv("POLYPOD_ANDROID_UPLOAD_KEY_ALIAS")
                keyPassword System.getenv("POLYPOD_ANDROID_UPLOAD_KEYSTORE_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            if (project.hasProperty("sign")) {
                signingConfig signingConfigs.playStoreUpload
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            assets.srcDirs += file('build/generated-assets')
            java.srcDirs += file('src/main/kotlin')
        }
        test {
            java.srcDirs += file('src/test/kotlin')
        }
        androidTest {
            java.srcDirs += file('src/androidTest/kotlin')
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                systemProperty 'robolectric.logging.enabled', 'true'
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation project(":bubblewrap")
    implementation project(":polyOut")
    implementation 'org.msgpack:msgpack-core:0.8.20'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.webkit:webkit:1.3.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.synnapps:carouselview:0.1.5'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation group: 'org.apache.jena', name: 'jena-core', version: '3.17.0'
    implementation 'androidx.work:work-runtime:2.3.4'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha03'

    // this unfortunately is needed to test the app
    debugImplementation 'androidx.test:core:1.3.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.2.5'

    // dependencies for tests that do not require Android device/emulator running
    testImplementation 'junit:junit:4.13'   // yuck...
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation 'androidx.test:rules:1.3.0'
    testImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    // fragment-testing requires entry with 'debugImplementation' scope, but also with 'testImplementation'
    // linter tells more (hence ignoring FragmentGradleConfiguration linter option)
    //noinspection FragmentGradleConfiguration
    testImplementation 'androidx.fragment:fragment-testing:1.2.5'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.30'

    // instrumented tests dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    // fragment-testing requires entry with 'debugImplementation' scope, but also with 'androidTestImplementation'
    // linter tells more (hence ignoring FragmentGradleConfiguration linter option)
    //noinspection FragmentGradleConfiguration
    androidTestImplementation 'androidx.fragment:fragment-testing:1.2.5'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestUtil('androidx.test:orchestrator:1.4.0')

    // Encryption related imports
    implementation("androidx.security:security-crypto:1.1.0-alpha03")
    // For Identity Credential APIs
    implementation("androidx.security:security-identity-credential:1.0.0-alpha02")
    // For App Authentication APIs
    implementation("androidx.security:security-app-authenticator:1.0.0-alpha02")
    // For App Authentication API testing
    androidTestImplementation("androidx.security:security-app-authenticator:1.0.0-alpha02")
}

task copyRemotePod(type: Copy) {
    from "../../feature-api/communication/remote-pod/dist/bootstrap.js"
    rename "bootstrap.js", "pod.js"
    into "build/generated-assets/container/"
}

task copyBundledFeatures(type: Copy) {
    from "../../../features/bundle/dist"
    into "build/generated-assets/features"
}

task copyThirdPartyLicenses(type: Copy) {
    from "../../../3rd-party-licenses"
    into "build/generated-assets/3rd-party-licenses"
}

task installAppOnDevice {
    doLast {
        exec {
            logger.info("Trying to install app")
            commandLine adb, 'devices'
            commandLine adb, 'install', '-r', "$buildDir/outputs/androidTest/debug/app-debug-androidTest.apk"
            logger.info("Application installed onto the device")
        }
    }
}

task packageTestFeature(type: Zip) {
    dependsOn installAppOnDevice
    archiveFileName = "test.zip"
    destinationDirectory = file("$buildDir")
    from "$projectDir/../../../features/test/dist"
}

task copyTestFeatureOntoDevice {
    dependsOn packageTestFeature
    def adb = android.getAdbExecutable().absolutePath
    def tempPath = "/data/local/tmp/test.zip"
    def packageName = android.defaultConfig.applicationId
    def featurePath = "files/features"
    doLast {
        exec {
            commandLine adb, 'push', "--sync", "$buildDir/test.zip", tempPath
            logger.info("test feature copied onto the device")
        }
        exec {
            commandLine adb, 'shell', "run-as", packageName, "mkdir", '-p', featurePath
            logger.info("Feature directory created on the device")
        }
        exec {
            commandLine adb, 'shell', "run-as", packageName, "cp", tempPath, featurePath
            logger.info("test feature moved to features directory on the device")
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == "generateDebugAssets" || task.name == "generateReleaseAssets") {
        task.dependsOn copyRemotePod
        task.dependsOn copyBundledFeatures
        task.dependsOn copyThirdPartyLicenses
    }
    if (
        task.name == "connectedDebugAndroidTest" ||
        task.name == "connectedReleaseAndroidTest" ||
        task.name == "generateDebugAndroidTestAssets"
    )
        task.dependsOn copyTestFeatureOntoDevice
}
